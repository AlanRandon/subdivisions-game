SELECT
	?country
	?division
	?preferredDivisionLabel
	(GROUP_CONCAT(DISTINCT ?divisionLabel; separator="$DIVIDE$") AS ?divisionLabels)
	(SAMPLE(?geoshapeValue) AS ?geo)
	(SAMPLE(?osmRelation) AS ?osm)
WHERE {
	SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en,mul".
		?country rdfs:label ?countryLabel.
		?division rdfs:label ?preferredDivisionLabel.
      
	}.

	# 10 most populated countries
	VALUES (?country) {
		(wd:Q148) # China
		(wd:Q668) # India
		(wd:Q30) # USA
		(wd:Q252) # Indonesia
		(wd:Q843) # Pakistan
		(wd:Q1033) # Nigeria
		(wd:Q155) # Brazil
		(wd:Q902) # Bangladesh
		(wd:Q159) # Russia
		(wd:Q155) # Ethiopia
	}

	?country wdt:P150 ?division.
  
	{ ?division rdfs:label ?divisionLabel FILTER (LANG(?divisionLabel) = "en") }
	UNION
	{ ?division skos:altLabel ?divisionLabel FILTER (LANG(?divisionLabel) = "en") }

  	OPTIONAL { ?division wdt:P402 ?osmRelation }.
	OPTIONAL { ?division wdt:P3896 ?geoshapeValue }.
}
GROUP BY ?country ?division ?preferredDivisionLabel
